/*
 * #%L
 * JSQLParser library
 * %%
 * Copyright (C) 2004 - 2014 JSQLParser
 * %%
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 2.1 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Lesser Public License for more details.
 *
 * You should have received a copy of the GNU General Lesser Public
 * License along with this program.  If not, see
 * <http://www.gnu.org/licenses/lgpl-2.1.html>.
 * #L%
 */

options{
    IGNORE_CASE=true ;
    STATIC=false;
//	DEBUG_PARSER=true;
//  DEBUG_LOOKAHEAD=true ;
//  FORCE_LA_CHECK=true;
//  DEBUG_TOKEN_MANAGER=true;
    UNICODE_INPUT=true;
}

PARSER_BEGIN(CCJSqlParser)
/*
 * #%L
 * JSQLParser library
 * %%
 * Copyright (C) 2004 - 2014 JSQLParser
 * %%
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 2.1 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Lesser Public License for more details.
 *
 * You should have received a copy of the GNU General Lesser Public
 * License along with this program.  If not, see
 * <http://www.gnu.org/licenses/lgpl-2.1.html>.
 * #L%
 */

package net.sf.jsqlparser.parser;

import net.sf.jsqlparser.expression.*;
import net.sf.jsqlparser.expression.operators.arithmetic.*;
import net.sf.jsqlparser.expression.operators.conditional.*;
import net.sf.jsqlparser.expression.operators.relational.*;
import net.sf.jsqlparser.schema.*;
import net.sf.jsqlparser.statement.*;
import net.sf.jsqlparser.statement.alter.*;
import net.sf.jsqlparser.statement.create.index.*;
import net.sf.jsqlparser.statement.create.table.*;
import net.sf.jsqlparser.statement.create.view.*;
import net.sf.jsqlparser.statement.delete.*;
import net.sf.jsqlparser.statement.drop.*;
import net.sf.jsqlparser.statement.insert.*;
import net.sf.jsqlparser.statement.replace.*;
import net.sf.jsqlparser.statement.select.*;
import net.sf.jsqlparser.statement.truncate.*;
import net.sf.jsqlparser.statement.update.*;

import java.util.*;

/**
 * The parser generated by JavaCC
 */
public class CCJSqlParser {
	private boolean allowOraclePrior = false;
}

PARSER_END(CCJSqlParser)

SKIP:
{
    " "
|   "\t"
|   "\r"
|   "\n"
}

TOKEN: /* SQL Keywords. prefixed with K_ to avoid name clashes */
{
    <K_AS: "AS">
|   <K_BY:"BY">
|   <K_DO:"DO">
|   <K_IS:"IS">
|   <K_IN:"IN">
|   <K_OR:"OR">
|   <K_ON:"ON">
|   <K_ALL:"ALL">
|   <K_AND:"AND">
|   <K_ANY:"ANY">
|   <K_KEY:"KEY">
|   <K_NOT:"NOT">
|   <K_SET:"SET">
|   <K_ASC:"ASC">
|   <K_TOP:"TOP">
|   <K_PERCENT:"PERCENT">
|   <K_END:"END">
|   <K_DESC:"DESC">
|   <K_INTO:"INTO">
|   <K_NULL:"NULL">
|   <K_LIKE:"LIKE">
|   <K_DROP:"DROP">
|   <K_JOIN:"JOIN">
|   <K_LEFT:"LEFT">
|   <K_CROSS:"CROSS">
|   <K_FROM:"FROM">
|   <K_OPEN:"OPEN">
|   <K_CASE:"CASE">
|   <K_WHEN:"WHEN">
|   <K_THEN:"THEN">
|   <K_ELSE:"ELSE">
|   <K_SOME:"SOME">
|   <K_FULL:"FULL">
|   <K_WITH:"WITH">
|   <K_TABLE:"TABLE">
|   <K_VIEW:"VIEW">
|   <K_WHERE:"WHERE">
|   <K_FOR:"FOR">
|   <K_PIVOT:"PIVOT">
|   <K_XML:"XML">
|   <K_USING:"USING">
|   <K_UNION:"UNION">
|   <K_GROUP:"GROUP">
|   <K_BEGIN:"BEGIN">
|   <K_INDEX: "INDEX">
|   <K_INNER:"INNER">
|   <K_LIMIT:"LIMIT">
|   <K_OUTER:"OUTER">
|   <K_ORDER:"ORDER">
|   <K_RIGHT:"RIGHT">
|   <K_VALUE:"VALUE">
|   <K_DELETE:"DELETE">
|   <K_CREATE:"CREATE">
|   <K_SELECT:"SELECT">
|   <K_OFFSET:"OFFSET">
|   <K_EXISTS:"EXISTS">
|   <K_HAVING:"HAVING">
|   <K_INSERT:"INSERT">
|   <K_UPDATE:"UPDATE">
|   <K_VALUES:"VALUES">
|   <K_ESCAPE:"ESCAPE">
|   <K_PRIMARY:"PRIMARY">
|   <K_NATURAL:"NATURAL">
|   <K_REPLACE:"REPLACE">
|   <K_BETWEEN:"BETWEEN">
|   <K_TRUNCATE:"TRUNCATE">
|   <K_DISTINCT:"DISTINCT">
|   <K_INTERSECT:"INTERSECT">
|   <K_CAST:"CAST">
|   <K_EXCEPT:"EXCEPT">
|   <K_MINUS:"MINUS">
|   <K_OVER:"OVER">
|   <K_PARTITION:"PARTITION">
|   <K_EXTRACT:"EXTRACT">
|	<K_LATERAL:"LATERAL">
|	<K_MATERIALIZED:"MATERIALIZED">
|   <K_INTERVAL:"INTERVAL">
|	<K_FOREIGN:"FOREIGN">
|	<K_CONSTRAINT:"CONSTRAINT">
|	<K_REFERENCES:"REFERENCES">
|	<K_CHARACTER:"CHARACTER">
|	<K_VARYING:"VARYING">
|	<K_START:"START">
|	<K_CONNECT:"CONNECT">
|	<K_PRIOR:"PRIOR">
|   <K_NOCYCLE:"NOCYCLE">
|	<K_SIBLINGS:"SIBLINGS">
|	<K_ALTER:"ALTER">
|	<K_ADD:"ADD">
|	<K_COLUMN:"COLUMN">
|   <K_NULLS: "NULLS">
|   <K_FIRST: "FIRST">
|   <K_LAST: "LAST">
|   <K_ROWS: "ROWS">
|   <K_RANGE: "RANGE">
|   <K_UNBOUNDED: "UNBOUNDED"> 
|   <K_PRECEDING: "PRECEDING">
|   <K_FOLLOWING: "FOLLOWING">
|   <K_CURRENT: "CURRENT">
|   <K_ROW: "ROW"> 
}

TOKEN : /* Numeric Constants */
{
   < S_DOUBLE: ((<S_LONG>)? "." <S_LONG> ( ["e","E"] (["+", "-"])? <S_LONG>)?
			|
			<S_LONG> "." (["e","E"] (["+", "-"])? <S_LONG>)?
			|
			<S_LONG> ["e","E"] (["+", "-"])? <S_LONG>
			)>
  | 	< S_LONG: ( <DIGIT> )+ >
  | 	< #DIGIT: ["0" - "9"] >
}

SPECIAL_TOKEN:
{
   < LINE_COMMENT: "--"(~["\r","\n"])*>
|  < MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN:
{
	< S_IDENTIFIER: ( <LETTER> | <ADDITIONAL_LETTERS> )+ ( <DIGIT> | <LETTER> | <ADDITIONAL_LETTERS> | <SPECIAL_CHARS>)* >
| 	< #LETTER: ["a"-"z", "A"-"Z", "_"] >
|   < #SPECIAL_CHARS: "$" | "_" | "#" | "@">
|   < S_CHAR_LITERAL: "'" (~["'"])* "'" ("'" (~["'"])* "'")*>
|   < S_QUOTED_IDENTIFIER: "\"" (~["\n","\r","\""])* "\"" | ("`" (~["\n","\r","`"])* "`") | ("[" (~["\n","\r","]"])* "]") >

/*
To deal with database names (columns, tables) using not only latin base characters, one
can expand the following rule to accept additional letters. Here is the addition of german umlauts.

There seems to be no way to recognize letters by an external function to allow
a configurable addition. One must rebuild JSqlParser with this new "Letterset".
*/
|   < #ADDITIONAL_LETTERS: ["ä","ö","ü","Ä","Ö","Ü","ß"] >
}

Statement Statement() : 
{ Statement stm; } 
{
    stm = SingleStatement()
    [";"]
    <EOF>
    { return stm; }
}

Statement SingleStatement() :
{ Statement stm;}
{
	(
    stm = Select()
    |
    stm =  Update()
    |
    stm = Insert()
    |
    stm = Delete()
    |
    stm = Replace()
    |
	stm = Alter()
	|
    LOOKAHEAD(3)
    stm = CreateIndex()
    |
	LOOKAHEAD(2)
    stm = CreateTable()
    |
	LOOKAHEAD(2)
	stm = CreateView()
	|
    stm = Drop()
    |
    stm = Truncate()
    )
    { return stm; }
}

Statements Statements() : 
{ Statements stmts = new Statements();
  List<Statement> list = new ArrayList<Statement>();
  Statement stm; }
{
    stm = SingleStatement() { list.add(stm); }
     (LOOKAHEAD(2) ";" stm = SingleStatement() { list.add(stm); } )*

    [";"]
    <EOF>

    {
        stmts.setStatements(list);
        return stmts;
    }
}

Update Update():
{
	Update update = new Update();
    Table table = null;
	List<Table> tables = new ArrayList<Table>();
	Expression where = null;
	Column tableColumn = null;
	List<Expression> expList = new ArrayList<Expression>();
	List<Column> columns = new ArrayList<Column>();
	Expression value = null;
	FromItem fromItem = null;
	List<Join> joins = null;
}
{
    <K_UPDATE> table=TableWithAlias() { tables.add(table); } 
          ("," table=TableWithAlias() { tables.add(table); } )*
    <K_SET> tableColumn=Column() "=" value=SimpleExpression() { columns.add(tableColumn); expList.add(value); }
		  ("," tableColumn=Column() "=" value=SimpleExpression()  { columns.add(tableColumn); expList.add(value); } )*

   [ <K_FROM>
      fromItem=FromItem()
      joins=JoinsList() ]

   [ where=WhereClause() { update.setWhere(where); } ]
   {
   		update.setColumns(columns);
   		update.setExpressions(expList);
   		update.setTables(tables);
		update.setFromItem(fromItem);
		update.setJoins(joins);
		return update;
   }
}

Replace Replace():
{
	Replace replace = new Replace();
	Table table = null;
	Column tableColumn = null;
	Expression value = null;

	List<Column> columns = new ArrayList<Column>();
	List<Expression> expList = new ArrayList<Expression>();
	ItemsList itemsList = null;
	Expression exp = null;
}
{
    <K_REPLACE> [<K_INTO>] table=Table()

    (
		(
		    <K_SET> tableColumn=Column() "=" value=SimpleExpression() { columns.add(tableColumn); expList.add(value); }
			  ("," tableColumn=Column() "=" value=SimpleExpression() { columns.add(tableColumn); expList.add(value); } )*
			 {
		   		replace.setExpressions(expList);
			 }
		)
		|

		(
		     [LOOKAHEAD(2) "(" tableColumn=Column() { columns.add(tableColumn); } ("," tableColumn=Column() { columns.add(tableColumn); } )* ")"  ]
			(
				LOOKAHEAD(2) [<K_VALUES> | <K_VALUE>] "(" exp=PrimaryExpression() { expList.add(exp); }
						("," exp=PrimaryExpression()  { expList.add(exp); } )* ")" { itemsList = new ExpressionList(expList); }
				|
					{ replace.setUseValues(false); }
				    itemsList=SubSelect()
                    { ((SubSelect)itemsList).setUseBrackets(false); }
			)
		    {
				replace.setItemsList(itemsList);
		    }
		)
	)
   {
   		if (columns.size() > 0)
	   		replace.setColumns(columns);
   		replace.setTable(table);
		return replace;
   }
}

Insert Insert():
{
	Insert insert = new Insert();
	Table table = null;
	Column tableColumn = null;
 	List<Column> columns = new ArrayList<Column>();
	List<Expression> primaryExpList = new ArrayList<Expression>();
	ItemsList itemsList = null;
	Expression exp = null;
	MultiExpressionList multiExpr = null;
}
{
    <K_INSERT> [<K_INTO>] table=Table()


     [LOOKAHEAD(2) "(" tableColumn=Column() { columns.add(tableColumn); } ("," tableColumn=Column() { columns.add(tableColumn); } )* ")"  ]
	(
		LOOKAHEAD(2) [<K_VALUES> | <K_VALUE>]  "(" exp=SimpleExpression() { primaryExpList.add(exp); }
				("," exp=SimpleExpression()  { primaryExpList.add(exp); } )* ")" { itemsList = new ExpressionList(primaryExpList); }
			("," "(" exp=SimpleExpression() {
					if (multiExpr==null) {
						multiExpr=new MultiExpressionList();
						multiExpr.addExpressionList((ExpressionList)itemsList);
						itemsList = multiExpr;
					}
					primaryExpList = new ArrayList<Expression>();
					primaryExpList.add(exp); }
				("," exp=SimpleExpression() { primaryExpList.add(exp); } )* ")" { multiExpr.addExpressionList(primaryExpList); } )*

		|

		[LOOKAHEAD(2) "(" ]
		(
			{ insert.setUseValues(false); }
			itemsList=  SubSelect()
		)
		[ ")" ]
	)

    {
		insert.setItemsList(itemsList);
	    insert.setTable(table);
	    if (columns.size() > 0)
	    	insert.setColumns(columns);
    	return insert;
    }
}

Delete Delete():
{
	Delete delete = new Delete();
	Table table = null;
	Expression where = null;
}
{
    <K_DELETE> [<K_FROM>] table=TableWithAlias()
    [where=WhereClause() { delete.setWhere(where); } ]
    {
    	delete.setTable(table);
    	return delete;
    }
}

// See: http://technet.microsoft.com/en-us/library/ms187879%28v=sql.105%29.aspx

Column Column():
{
	String databaseName = null, schemaName = null, tableName = null, columnName = null;
}
{
    (
          LOOKAHEAD(7) databaseName=RelObjectName() "." [schemaName=RelObjectName()] "." tableName=RelObjectName() "." columnName=RelObjectName()
        | LOOKAHEAD(5) schemaName=RelObjectName() "." tableName=RelObjectName() "." columnName=RelObjectName()
        | LOOKAHEAD(3) tableName=RelObjectName() "." columnName=RelObjectName()
        | columnName=RelObjectName()
    )
	{
        final Database database = new Database(databaseName);
        final Table table = new Table(database, schemaName, tableName);
    	return new Column(table, columnName);
	}
}

String RelObjectName():
{	Token tk = null; }
{
	(tk=<S_IDENTIFIER> | tk=<S_QUOTED_IDENTIFIER>
      | tk=<K_CAST> | tk=<K_DO> | tk=<K_EXTRACT> | tk=<K_FIRST> | tk=<K_FOLLOWING> | tk=<K_LAST> | tk=<K_MATERIALIZED> | tk=<K_NULLS> | tk=<K_PARTITION> | tk=<K_RANGE> | tk=<K_ROW> | tk=<K_ROWS> | tk=<K_SIBLINGS> | tk=<K_VALUE> | tk=<K_XML>)

    { return tk.image; }
}

Table Table():
{
	String serverName = null, databaseName = null, schemaName = null, tableName = null;
}
{
    (
          LOOKAHEAD(7) serverName=RelObjectName() "." [databaseName=RelObjectName()] "." [schemaName=RelObjectName()] "." tableName=RelObjectName()
        | LOOKAHEAD(5) databaseName=RelObjectName() "." [schemaName=RelObjectName()] "." tableName=RelObjectName()
        | LOOKAHEAD(3) schemaName=RelObjectName() "." tableName=RelObjectName()
        | tableName=RelObjectName()
    )
	{
        final Server server = new Server(serverName);
        final Database database = new Database(server, databaseName);
    	return new Table(database, schemaName, tableName);
	}
}

Table TableWithAlias():
{
	Table table = null;
	Alias alias = null;
}
{
	table=Table() [alias=Alias() { table.setAlias(alias); }]
	{ return table; }
}

Select Select():
{
	Select select = new Select();
	SelectBody selectBody = null;
	List<WithItem> with = null;
}
{
	[ with=WithList() { select.setWithItemsList(with); } ]
    selectBody = SelectBody()
	{
		select.setSelectBody(selectBody);
		return select;
	}
}

SelectBody SelectBody():
{ SelectBody selectBody = null; }
{
	(
		LOOKAHEAD(SetOperationList())
		    selectBody = SetOperationList()
    	|
	    selectBody = PlainSelect()
	)
	{ return selectBody; }
}

PlainSelect PlainSelect():
{
	PlainSelect plainSelect = new PlainSelect();
	List<SelectItem> selectItems = null;
	FromItem fromItem = null;
	List<Join> joins = null;
	List<SelectItem> distinctOn = null;
	Expression where = null;
	List<OrderByElement> orderByElements;
	List<Expression> groupByColumnReferences = null;
	Expression having = null;
	Limit limit = null;
	Top top = null;
	OracleHierarchicalExpression oracleHierarchicalQueryClause = null;
}
{
    <K_SELECT>

    [
    	<K_ALL>
    	|
    		(
    			<K_DISTINCT> { Distinct distinct = new Distinct(); plainSelect.setDistinct(distinct); }
    				[ "ON" "(" distinctOn=SelectItemsList()  { plainSelect.getDistinct().setOnSelectItems(distinctOn); } ")" ]
    		)
    ]

    [top = Top() { plainSelect.setTop(top);	} ]


    selectItems=SelectItemsList()

     // TODO
    [IntoClause()]
    [ <K_FROM>
      fromItem=FromItem()
      joins=JoinsList() ]

    [ where=WhereClause() { plainSelect.setWhere(where); }]
	[ oracleHierarchicalQueryClause=OracleHierarchicalQueryClause() { plainSelect.setOracleHierarchical(oracleHierarchicalQueryClause); } ]
    [ groupByColumnReferences=GroupByColumnReferences() { plainSelect.setGroupByColumnReferences(groupByColumnReferences); }]
    [ having=Having() { plainSelect.setHaving(having); }]
	[LOOKAHEAD(<K_ORDER> <K_SIBLINGS> <K_BY>) orderByElements = OrderByElements() { plainSelect.setOracleSiblings(true); plainSelect.setOrderByElements(orderByElements);	}   ]
	[LOOKAHEAD(<K_ORDER> <K_BY>) orderByElements = OrderByElements() { plainSelect.setOrderByElements(orderByElements);	}   ]
    [LOOKAHEAD(2) limit = Limit() { plainSelect.setLimit(limit);	} ]

	{
		plainSelect.setSelectItems(selectItems);
		plainSelect.setFromItem(fromItem);
		if (joins != null && joins.size() > 0)
			plainSelect.setJoins(joins);
		return plainSelect;
	}
}

SetOperationList SetOperationList():
{
	SetOperationList list = new SetOperationList();
	List<OrderByElement> orderByElements = null;
	Limit limit = null;
	PlainSelect select = null;
	List<PlainSelect> selects = new ArrayList<PlainSelect>();
	List<SetOperation> operations = new ArrayList<SetOperation>();
}
{
	(

		(
			(("(" select=PlainSelect() ")") | (select=PlainSelect() )) {selects.add(select);}
			(
				((<K_UNION> { UnionOp union = new UnionOp();operations.add(union); } [ <K_ALL> { union.setAll(true); } | <K_DISTINCT> { union.setDistinct(true); } ])
				| <K_INTERSECT> { operations.add(new IntersectOp()); }
				| <K_MINUS> { operations.add(new MinusOp()); }
				| <K_EXCEPT> { operations.add(new ExceptOp()); }
				)

				(("(" select=PlainSelect() ")") | (select=PlainSelect() )) {selects.add(select);}
			)+
		)

		[orderByElements=OrderByElements() {list.setOrderByElements(orderByElements);} ]
		[limit=Limit() {list.setLimit(limit);} ]
	)

	{
		list.setOpsAndSelects(selects,operations);
		return list;
	}
}

List<WithItem> WithList():
{
	List<WithItem> withItemsList = new ArrayList<WithItem>();
	WithItem with = null;
}
{
	<K_WITH> with=WithItem() { withItemsList.add(with); } ("," with=WithItem() { withItemsList.add(with); } )*

 	{ return withItemsList; }
}

WithItem WithItem():
{
	WithItem with = new WithItem();
	String name = null;
	List<SelectItem> selectItems = null;
	SelectBody selectBody = null;
}
{
	 name=RelObjectName() { with.setName(name); }
	 [ "(" selectItems=SelectItemsList() ")" { with.setWithItemList(selectItems); } ]
	 <K_AS>
	 "(" selectBody = SelectBody() { with.setSelectBody(selectBody); } ")"
	 { return with; }
}

List<SelectItem> SelectItemsList():
{
	List<SelectItem> selectItemsList = new ArrayList<SelectItem>();
	SelectItem selectItem = null;
}
{
    selectItem=SelectItem() { selectItemsList.add(selectItem); } ("," selectItem=SelectItem() { selectItemsList.add(selectItem); } )*

    { return selectItemsList; }
}

SelectExpressionItem SelectExpressionItem():
{
	SelectExpressionItem selectExpressionItem = null;
	Expression expression = null;
	Alias alias = null;
}
{
	 expression=SimpleExpression() { selectExpressionItem = new SelectExpressionItem(); selectExpressionItem.setExpression(expression); }
			 [alias=Alias() { selectExpressionItem.setAlias(alias); }] { return selectExpressionItem; }
}

SelectItem SelectItem():
{
	SelectItem selectItem = null;
}
{   ("*" { selectItem = new AllColumns(); }
    |
	LOOKAHEAD(AllTableColumns()) selectItem=AllTableColumns()
	|
	LOOKAHEAD(SelectExpressionItem()) selectItem=SelectExpressionItem() 
	)
	{
		return selectItem;
	}
}

AllTableColumns AllTableColumns():
{
	Table table = null;
}
{
     table=Table() "." "*"
	{
		return new AllTableColumns(table);
	}

}

Alias Alias():
{ String name = null;
  boolean useAs = false; }
{
	[<K_AS> { useAs = true; } ] name=RelObjectName()
   { return new Alias(name,useAs); }
}

FunctionItem FunctionItem():
{
    Alias alias = null;
    Function function;
    FunctionItem functionItem;
}
{
    function=Function() { functionItem = new FunctionItem(); functionItem.setFunction(function); }
			 [alias=Alias() { functionItem.setAlias(alias); }]
    { return functionItem; }
}

List<Column> PivotForColumns():
{
    List<Column> columns = new ArrayList<Column>();
    Column column;
}
{
    (
		("(" column = Column() { columns.add(column); }
         ("," column = Column() { columns.add(column); } )*
		 ")")
	| column = Column() { columns.add(column); }
	)
    { return columns; }
}

List<FunctionItem> PivotFunctionItems():
{
    List<FunctionItem> functionItems = new ArrayList<FunctionItem>();
    FunctionItem item;
}
{
    item = FunctionItem() {functionItems.add(item);}
    ( "," item = FunctionItem() {functionItems.add(item);} )*
    { return functionItems; }
}

List<SelectExpressionItem> PivotSingleInItems():
{
   List<SelectExpressionItem> retval = new ArrayList<SelectExpressionItem>();
   SelectExpressionItem item;
}
{
   item = SelectExpressionItem() {retval.add(item);}
   ("," item = SelectExpressionItem() {retval.add(item);} )*
   { return retval; }
}

ExpressionListItem ExpressionListItem():
{
	ExpressionListItem expressionListItem = null;
	ExpressionList expressionList = null;
	Alias alias = null;
}
{
    "("
    expressionList=SimpleExpressionList() { expressionListItem = new ExpressionListItem(); expressionListItem.setExpressionList(expressionList); }
    ")"
	[alias=Alias() { expressionListItem.setAlias(alias); }]
    { return expressionListItem; }
}

List<ExpressionListItem> PivotMultiInItems():
{
   List<ExpressionListItem> retval = new ArrayList<ExpressionListItem>();
   ExpressionListItem item;
}
{
   item = ExpressionListItem() {retval.add(item);}
   ("," item = ExpressionListItem() {retval.add(item);} )*
   { return retval; }
}

Pivot Pivot():
{
    Pivot retval = new Pivot();
    List<FunctionItem> functionItems;
    List<Column> forColumns;
    List<SelectExpressionItem> singleInItems = null;
    List<ExpressionListItem> multiInItems = null;
}
{
	<K_PIVOT> "(" functionItems = PivotFunctionItems() <K_FOR>
	forColumns = PivotForColumns()
	<K_IN> "("
	(LOOKAHEAD(3) singleInItems = PivotSingleInItems()
	| multiInItems = PivotMultiInItems() )
	")"
	")"
    {
        retval.setFunctionItems(functionItems);
        retval.setForColumns(forColumns);
        retval.setSingleInItems(singleInItems);
        retval.setMultiInItems(multiInItems);
        return retval;
    }
}

PivotXml PivotXml():
{
    PivotXml retval = new PivotXml();
    List<FunctionItem> functionItems;
    List<Column> forColumns;
    List<SelectExpressionItem> singleInItems = null;
    List<ExpressionListItem> multiInItems = null;
    SelectBody inSelect = null;
}
{
	<K_PIVOT> <K_XML> "(" functionItems = PivotFunctionItems() <K_FOR>
	forColumns = PivotForColumns()
	<K_IN> "("
	(
	    <K_ANY> { retval.setInAny(true); } |
	    LOOKAHEAD(1) inSelect = SelectBody() |
	    LOOKAHEAD(2) singleInItems = PivotSingleInItems() |
	    multiInItems = PivotMultiInItems()
    )
	")"
	")"
    {
        retval.setFunctionItems(functionItems);
        retval.setForColumns(forColumns);
        retval.setSingleInItems(singleInItems);
        retval.setMultiInItems(multiInItems);
        retval.setInSelect(inSelect);
        return retval;
    }
}

void IntoClause():
{}
{
   <K_INTO> Table() ("," Table())*
}

FromItem FromItem():
{
	FromItem fromItem = null;
	Pivot pivot = null;
	Alias alias = null;
}
{
	(
		LOOKAHEAD(ValuesList()) fromItem=ValuesList()
		|
		(
			(
				(
					"("
						(
						LOOKAHEAD(SubJoin())
						fromItem=SubJoin()
						|
						fromItem=SubSelect()
						)
					")"
				)
				|
				fromItem=Table()
				|
				fromItem=LateralSubSelect()
			)
			[(LOOKAHEAD(2) pivot=PivotXml()|pivot=Pivot()) { fromItem.setPivot(pivot); } ]
			[alias=Alias() { fromItem.setAlias(alias); } ]
		)
	)
	{
		return fromItem;
	}
}

FromItem ValuesList():
{
	MultiExpressionList exprList = new MultiExpressionList();
	List<Expression> primaryExpList = new ArrayList<Expression>();
	ValuesList valuesList = new ValuesList();
	Expression exp = null;
	List<String> colNames = null;
	String colName;
	Alias alias;
}
{
	"("
	<K_VALUES>
	(LOOKAHEAD(3) ("(" exp=SimpleExpression() { primaryExpList.add(exp); }
			("," exp=SimpleExpression()  { primaryExpList.add(exp); } )* ")" { exprList.addExpressionList(primaryExpList); }

			("," "(" exp=SimpleExpression() {
					primaryExpList = new ArrayList<Expression>();
					primaryExpList.add(exp); }
				("," exp=SimpleExpression() { primaryExpList.add(exp); } )* ")" { exprList.addExpressionList(primaryExpList); } )*)
	|
	( exp=SimpleExpression() { exprList.addExpressionList(exp); valuesList.setNoBrackets(true); }
		("," exp=SimpleExpression()  { exprList.addExpressionList(exp);} )*
	))
	")"

	[alias=Alias() { valuesList.setAlias(alias); }

		[ "("
			colName = RelObjectName() { colNames = new ArrayList<String>(); colNames.add(colName); }
			( "," colName = RelObjectName() { colNames.add(colName); } )*
			")" { valuesList.setColumnNames(colNames); } ]

	]

	{
		valuesList.setMultiExpressionList(exprList);
		return valuesList;
	}
}

LateralSubSelect LateralSubSelect():
{
	LateralSubSelect lateralSubSelect = new LateralSubSelect();
	SubSelect subSelect = null;
}
{
	<K_LATERAL>
	 "(" subSelect=SubSelect() ")"
	{
		lateralSubSelect.setSubSelect(subSelect);
		return lateralSubSelect;
	}
}

FromItem SubJoin():
{
	FromItem fromItem = null;
	Join join = null;
	SubJoin subJoin = new SubJoin();
}
{
	fromItem=FromItem() { subJoin.setLeft(fromItem); }
	join=JoinerExpression() { subJoin.setJoin(join); }

	{
		return subJoin;
	}
}

List JoinsList():
{
	List<Join> joinsList = new ArrayList<Join>();
	Join join = null;
}
{

    (join=JoinerExpression() { joinsList.add(join); })*

    { return joinsList; }
}

Join JoinerExpression():
{
	Join join = new Join();
	FromItem right = null;
	Expression onExpression = null;
	Column tableColumn;
	List<Column> columns = null;
}
{
	/*
		Refactor to be more restrictive.
			left [outer] join
			right [outer] join
			full [outer] join
			[inner] join
			cross join
			natural join
     */
	  [ (
	  	<K_LEFT> { join.setLeft(true); }
  		| <K_RIGHT> { join.setRight(true); }
  		| <K_FULL> { join.setFull(true); }
  		| <K_NATURAL> { join.setNatural(true); }
		| <K_CROSS> { join.setCross(true); }
  		)
  	   ]

  	   [
  	    (
  		<K_OUTER> { join.setOuter(true); }
  		| <K_INNER> { join.setInner(true); }
  		)
  	   ]

  	    ( <K_JOIN> | "," { join.setSimple(true); } ) right=FromItem()
	[
		( <K_ON> onExpression=Expression()  { join.setOnExpression(onExpression); } )
		|
		( <K_USING> "(" tableColumn=Column() { columns = new ArrayList(); columns.add(tableColumn); }
				("," tableColumn=Column() { columns.add(tableColumn); } )* ")"
		  { join.setUsingColumns(columns); }   )
  	]
  {
  	join.setRightItem(right);
  }


  { return join; }
}

Expression WhereClause():
{
	Expression retval = null;
}
{
    <K_WHERE> retval=Expression()
    { return retval; }
}

OracleHierarchicalExpression OracleHierarchicalQueryClause():
{
	OracleHierarchicalExpression result = new OracleHierarchicalExpression();
	Expression expr;
}
{
	[ <K_START> <K_WITH>  expr=AndExpression() {result.setStartExpression(expr);} ]
	<K_CONNECT> <K_BY> [ <K_NOCYCLE> { result.setNoCycle(true); } ]
	{ allowOraclePrior=true; } expr=AndExpression()
	{ result.setConnectExpression(expr);allowOraclePrior=false; }

	{ return result; }
}

List<Expression> GroupByColumnReferences():
{
	Expression columnReference = null;
	List<Expression> columnReferences = new ArrayList<Expression>();
}
{
    <K_GROUP> <K_BY> columnReference=SimpleExpression() {columnReferences.add(columnReference); }
    ("," columnReference=SimpleExpression()  {columnReferences.add(columnReference); } )*
	{
		return columnReferences;
	}
}

Expression Having():
{
	Expression having = null;
}
{
    <K_HAVING> having=Expression()
	{
		return having;
	}
}

List<OrderByElement> OrderByElements():
{
	List<OrderByElement> orderByList = new ArrayList<OrderByElement>();
	OrderByElement orderByElement = null;
}
{
    <K_ORDER> [ <K_SIBLINGS> ] <K_BY> orderByElement=OrderByElement() { orderByList.add(orderByElement); }
        ("," orderByElement=OrderByElement() { orderByList.add(orderByElement); } )*
    {
    	return orderByList;
    }
}

OrderByElement OrderByElement():
{
	OrderByElement orderByElement = new OrderByElement();
	Expression columnReference = null;
}
{
	columnReference = SimpleExpression()
    [ ( <K_ASC> | (<K_DESC> { orderByElement.setAsc(false); } )) { orderByElement.setAscDescPresent(true); }  ]
    [<K_NULLS> ( 
        <K_FIRST> { orderByElement.setNullOrdering(OrderByElement.NullOrdering.NULLS_FIRST);  }  | 
        <K_LAST> { orderByElement.setNullOrdering(OrderByElement.NullOrdering.NULLS_LAST);  }  
        )?
    ]
	{
    	orderByElement.setExpression(columnReference);
		return orderByElement;
	}
}

Limit Limit():
{
	Limit limit = new Limit();
	Token token = null;
}
{
	(
			LOOKAHEAD(3)
				// mysql-> LIMIT offset,row_count
				<K_LIMIT>
					 (
					 	token=<S_LONG> { limit.setOffset(Long.parseLong(token.image)); }
					 	|
					 	"?" { limit.setOffsetJdbcParameter(true);}

					 )
					 ","

				(
				token=<S_LONG> { limit.setRowCount(Long.parseLong(token.image)); } | "?" { limit.setRowCountJdbcParameter(true);}
				)
			|
			// postgresql-> OFFSET offset
			 <K_OFFSET>
				 (token=<S_LONG> { limit.setOffset(Long.parseLong(token.image)); } | "?" { limit.setOffsetJdbcParameter(true);} )
			|
				// mysql-postgresql-> LIMIT (row_count | ALL) [OFFSET offset]
				<K_LIMIT>
				 (
				 	token=<S_LONG> { limit.setRowCount(Long.parseLong(token.image)); }
				 	|
				 	"?" { limit.setRowCountJdbcParameter(true);}
				 	|
				 	<K_ALL> { limit.setLimitAll(true);}
				 )

				 [LOOKAHEAD(2) <K_OFFSET>
					 (token=<S_LONG> { limit.setOffset(Long.parseLong(token.image)); } | "?" { limit.setOffsetJdbcParameter(true);} )  ]

		)
	{
		return limit;
	}
}

// according to http://technet.microsoft.com/en-us/library/ms189463.aspx
Top Top():
{
	Top top = new Top();
	Token token = null;
}
{
	<K_TOP>
	(
	 	token=<S_LONG>                      { top.setRowCount(Long.parseLong(token.image)); }
	   |
	 	"?"                                 { top.setRowCountJdbcParameter(true);}
	   |
	   "("
            ( token=<S_LONG>                { top.setRowCount(Long.parseLong(token.image)); }
              | "?"                         { top.setRowCountJdbcParameter(true);} )
            { top.setParenthesis(true);}
       ")"
	) [ <K_PERCENT>                         { top.setPercentage(true); } ] 
	{
		return top;
	}
}

Expression Expression():
{
	Expression retval = null;
}
{
	(
	LOOKAHEAD(OrExpression())
		retval=OrExpression()
		|
		"(" retval=Expression() ")" {retval = new Parenthesis(retval); }

	)

    { return retval; }
}

Expression OrExpression():
{
	Expression left, right, result;
}
{
	left=AndExpression() { result = left; }
	(
		LOOKAHEAD(<K_OR>)
			<K_OR>
			right=AndExpression()
			{
				result = new OrExpression(left, right);
				left = result;
	   		}
	 )*
	 {
	 	return result;
	 }

}

Expression AndExpression() :
{
	Expression left, right, result;
	boolean not = false;
}
{
	(
	LOOKAHEAD(Condition())
	    left=Condition()
	    |
	    [ <K_NOT> { not = true; } ]
	    "(" left=OrExpression() ")" {left = new Parenthesis(left); if (not) { ((Parenthesis)left).setNot(); not = false; } }
	)
	{ result = left; }

	(
		LOOKAHEAD(<K_AND>)
 		<K_AND>
		(
		LOOKAHEAD(Condition())
		    right=Condition()
		    |
		    [ <K_NOT> { not = true; } ]
		    "(" right=OrExpression() ")" {right = new Parenthesis(right); if (not) { ((Parenthesis)right).setNot(); not = false; } }
		)
		{
			result = new AndExpression(left, right);
			left = result;
		}
	)*
	{
		return result;
	}
}

Expression Condition():
{
	Expression result;
}
{
	(LOOKAHEAD(SQLCondition()) result=SQLCondition()
	| LOOKAHEAD(RegularCondition()) result=RegularCondition()
	| result=Function()
	)

	{ return result; }
}

Expression RegularCondition():
{
	Expression result = null;
	Expression leftExpression;
	Expression rightExpression;
	boolean not = false;
	int oracleJoin=EqualsTo.NO_ORACLE_JOIN;
	int oraclePrior=EqualsTo.NO_ORACLE_PRIOR;
}
{
	[ <K_PRIOR> { oraclePrior = EqualsTo.ORACLE_PRIOR_START; }]
	[ <K_NOT> { not = true; } ]
	leftExpression=ComparisonItem() { result = leftExpression; }

	[ "(+)" { oracleJoin=EqualsTo.ORACLE_JOIN_RIGHT; } ]

	(
	">" { result = new GreaterThan(); }
	| "<" { result = new MinorThan(); }
	| "=" { result = new EqualsTo(); }
	| ">=" { result = new GreaterThanEquals(); }
	| "<=" { result = new MinorThanEquals(); }
	| "<>" { result = new NotEqualsTo(); }
    | "!=" { result = new NotEqualsTo("!="); }
	| "@@" { result = new Matches(); }
	| "~" { result = new RegExpMatchOperator(RegExpMatchOperatorType.MATCH_CASESENSITIVE); }
	| "~*" { result = new RegExpMatchOperator(RegExpMatchOperatorType.MATCH_CASEINSENSITIVE); }
	| "!~" { result = new RegExpMatchOperator(RegExpMatchOperatorType.NOT_MATCH_CASESENSITIVE); }
	| "!~*" { result = new RegExpMatchOperator(RegExpMatchOperatorType.NOT_MATCH_CASEINSENSITIVE); }
	)
	rightExpression=ComparisonItem()

	[ <K_PRIOR> { oraclePrior = EqualsTo.ORACLE_PRIOR_END; } ]
	[ "(+)" { oracleJoin=EqualsTo.ORACLE_JOIN_LEFT; } ]

	{
		BinaryExpression regCond = (BinaryExpression) result;
		regCond.setLeftExpression(leftExpression);
		regCond.setRightExpression(rightExpression);
		if (not)
			regCond.setNot();

		if (oracleJoin>0)
			((SupportsOldOracleJoinSyntax)result).setOldOracleJoinSyntax(oracleJoin);

		if (oraclePrior!=EqualsTo.NO_ORACLE_PRIOR)
			((SupportsOldOracleJoinSyntax)result).setOraclePriorPosition(oraclePrior);
	}

	{ return result; }
}

Expression SQLCondition():
{
	Expression result;
}
{
	(
	LOOKAHEAD(InExpression()) result=InExpression()
	| LOOKAHEAD(Between()) result=Between()
	| LOOKAHEAD(IsNullExpression()) result=IsNullExpression()
	| LOOKAHEAD(ExistsExpression()) result=ExistsExpression()
	|  result=LikeExpression()
	)
	{ return result; }
}

Expression InExpression() :
{
	InExpression result = new InExpression();
	ItemsList leftItemsList = null;
	ItemsList rightItemsList = null;
	Expression leftExpression = null;
}
{
	( LOOKAHEAD(1) "(" (
			LOOKAHEAD(SimpleExpressionList()) leftItemsList = SimpleExpressionList() { result.setLeftItemsList(leftItemsList); }
			|
			leftExpression=SimpleExpression()
	        [ "(+)" { result.setOldOracleJoinSyntax(EqualsTo.ORACLE_JOIN_RIGHT); } ]
		  )
	  ")"
	|
		leftExpression=SimpleExpression() { result.setLeftExpression(leftExpression); }
        [ "(+)" { result.setOldOracleJoinSyntax(EqualsTo.ORACLE_JOIN_RIGHT); } ]
	)
    [<K_NOT> { result.setNot(true); } ] <K_IN> "(" (LOOKAHEAD(SubSelect()) rightItemsList=SubSelect() | rightItemsList=SimpleExpressionList() ) ")"
	{
		result.setRightItemsList(rightItemsList);
		return result;
	}
}

Expression Between() :
{
	Between result = new Between();
	Expression leftExpression = null;
	Expression betweenExpressionStart = null;
	Expression betweenExpressionEnd = null;
}
{
	leftExpression=SimpleExpression()
	    [<K_NOT> { result.setNot(true); }]
	    <K_BETWEEN> betweenExpressionStart=SimpleExpression() <K_AND> betweenExpressionEnd=SimpleExpression()

	{
		result.setLeftExpression(leftExpression);
		result.setBetweenExpressionStart(betweenExpressionStart);
		result.setBetweenExpressionEnd(betweenExpressionEnd);
		return result;
	}
}

Expression LikeExpression() :
{
	LikeExpression result = new LikeExpression();
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
	leftExpression=SimpleExpression()
    [<K_NOT> { result.setNot(true); } ] <K_LIKE> rightExpression=SimpleExpression()
    [<K_ESCAPE> token=<S_CHAR_LITERAL> { result.setEscape((new StringValue(token.image)).getValue()); }]
	{
		result.setLeftExpression(leftExpression);
		result.setRightExpression(rightExpression);
		return result;
	}
}

Expression IsNullExpression():
{
	IsNullExpression result = new IsNullExpression();
	Expression leftExpression = null;
}
{
	(
	<K_NOT> { result.setNot(true); } leftExpression=SimpleExpression() <K_IS> <K_NULL>
	|
	leftExpression=SimpleExpression() <K_IS> [<K_NOT> { result.setNot(true); } ] <K_NULL>
	)

	{
		result.setLeftExpression(leftExpression);
		return result;
	}
}

Expression ExistsExpression():
{
	ExistsExpression result = new ExistsExpression();
	Expression rightExpression = null;
}
{
    [<K_NOT> { result.setNot(true); } ] <K_EXISTS> rightExpression=SimpleExpression()
	{
		result.setRightExpression(rightExpression);
		return result;
	}
}

ExpressionList SQLExpressionList():
{
	ExpressionList retval = new ExpressionList();
	List<Expression> expressions = new ArrayList<Expression>();
	Expression expr = null;
}
{
    expr=Expression() { expressions.add(expr); } ("," expr=Expression() { expressions.add(expr); })*
	{
		retval.setExpressions(expressions);
		return retval;
	}
}

ExpressionList SimpleExpressionList():
{
	ExpressionList retval = new ExpressionList();
	List<Expression> expressions = new ArrayList<Expression>();
	Expression expr = null;
}
{
    expr=SimpleExpression() { expressions.add(expr); } ("," expr=SimpleExpression() { expressions.add(expr); })*
	{
		retval.setExpressions(expressions);
		return retval;
	}
}

Expression ComparisonItem() :
{
	Expression retval = null;
}
{
  (
    retval=AllComparisonExpression()
    | retval=AnyComparisonExpression()
    | retval=SimpleExpression()
  )

   {
      return retval;
   }
}

Expression AllComparisonExpression() :
{
	AllComparisonExpression retval = null;
	SubSelect subselect = null;
}
{
 <K_ALL> "(" subselect=SubSelect() ")" { retval = new AllComparisonExpression(subselect); }
   {
      return retval;
   }
}

Expression AnyComparisonExpression() :
{
	AnyComparisonExpression retval = null;
	SubSelect subselect = null;
}
{
 (<K_ANY> | <K_SOME>) "(" subselect=SubSelect() ")" { retval = new AnyComparisonExpression(subselect); }
   {
      return retval;
   }
}

Expression SimpleExpression():
{
	Expression retval = null;
}
{
  (
		LOOKAHEAD(BitwiseAndOr())
		retval=BitwiseAndOr()
		|
		"(" retval=BitwiseAndOr() ")" {retval = new Parenthesis(retval); }

  )

   {
      return retval;
   }
}

Expression ConcatExpression():
{
	Expression result = null;
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
    leftExpression=AdditiveExpression()  { result = leftExpression; }
    (
    	"||"
    	rightExpression=AdditiveExpression()
		{
			Concat binExp = new Concat();
			binExp.setLeftExpression(leftExpression);
			binExp.setRightExpression(rightExpression);
			result = binExp;
			leftExpression = result;
		}
    )*

    { return result; }
}

Expression BitwiseAndOr():
{
	Expression result = null;
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
    leftExpression=ConcatExpression()  { result = leftExpression; }
    (
    	LOOKAHEAD(2) (
    					"|" { result = new BitwiseOr(); }
    				  	|
    				  	"&" { result = new BitwiseAnd(); }
    				  )

    						rightExpression=ConcatExpression()

		{
			BinaryExpression binExp = (BinaryExpression) result;
			binExp.setLeftExpression(leftExpression);
			binExp.setRightExpression(rightExpression);
			leftExpression = result;
		}
    )*

    { return result; }
}

Expression AdditiveExpression():
{
	Expression result = null;
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
    leftExpression=MultiplicativeExpression()  { result = leftExpression; }
    (
    	LOOKAHEAD(2) ("+" { result = new Addition(); }
    								| "-" { result = new Subtraction(); } )

        rightExpression=MultiplicativeExpression()
		{
			BinaryExpression binExp = (BinaryExpression) result;
			binExp.setLeftExpression(leftExpression);
			binExp.setRightExpression(rightExpression);
			leftExpression = result;
		}
    )*

    { return result; }
}

Expression MultiplicativeExpression():
{
	Expression result = null;
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
	(
	LOOKAHEAD(BitwiseXor())
	    leftExpression=BitwiseXor()
	    |
	    "(" leftExpression=ConcatExpression() ")" {leftExpression = new Parenthesis(leftExpression); }
	)
      { result = leftExpression; }
    (
    	LOOKAHEAD(2) ("*" { result = new Multiplication(); }
    								| "/" { result = new Division(); }
    								| "%" { result = new Modulo(); }
    				)

				(
				LOOKAHEAD(BitwiseXor())
    						rightExpression=BitwiseXor()
						    |
						    "(" rightExpression=ConcatExpression() ")" {rightExpression = new Parenthesis(rightExpression); }
				)

		{
			BinaryExpression binExp = (BinaryExpression) result;
			binExp.setLeftExpression(leftExpression);
			binExp.setRightExpression(rightExpression);
			leftExpression = result;
		}
    )*
    { return result; }
}

Expression BitwiseXor():
{
	Expression result = null;
	Expression leftExpression = null;
	Expression rightExpression = null;
}
{
    leftExpression=PrimaryExpression()  { result = leftExpression; }
    (
    	"^"
    	rightExpression=PrimaryExpression()
		{
			BitwiseXor binExp = new BitwiseXor();
			binExp.setLeftExpression(leftExpression);
			binExp.setRightExpression(rightExpression);
			result = binExp;
			leftExpression = result;
		}
    )*

    { return result; }
}

Expression PrimaryExpression():
{
	Expression retval = null;
    CastExpression castExpr = null;
	Token token = null;
	Token sign = null;
	String tmp = "";
	ColDataType type = null;
}
{
(
	<K_NULL> { retval = new NullValue(); }

	| retval=CaseWhenExpression()

	| "?" { retval = new JdbcParameter(); }

    | retval=JdbcNamedParameter()

	| LOOKAHEAD(AnalyticExpression()) retval=AnalyticExpression()

	| LOOKAHEAD(ExtractExpression()) retval=ExtractExpression()

	| LOOKAHEAD(["+" | "-"] Function())       [sign="+" | sign="-"] retval=Function()

	| LOOKAHEAD(["+" | "-"] <S_DOUBLE>)       [sign="+" | sign="-"] token=<S_DOUBLE>  { retval = new DoubleValue(token.image); }

	| LOOKAHEAD(["+" | "-"] <S_LONG>)         [sign="+" | sign="-"] token=<S_LONG> { retval = new LongValue(token.image); }

	| LOOKAHEAD(["+" | "-"] CastExpression()) [sign="+" | sign="-"] retval=CastExpression()

	| LOOKAHEAD(["+" | "-"] Column())         [sign="+" | sign="-"] retval=Column()

	| LOOKAHEAD(2)                            [sign="+" | sign="-"] "(" retval=PrimaryExpression() ")" {retval = new Parenthesis(retval); }

	|                                         [sign="+" | sign="-"] "(" retval=SubSelect() ")"

	| token=<S_CHAR_LITERAL> { retval = new StringValue(token.image); }


	| "{d" token=<S_CHAR_LITERAL> "}"  { retval = new DateValue(token.image); }

	| "{t" token=<S_CHAR_LITERAL> "}"  { retval = new TimeValue(token.image); }

	| "{ts" token=<S_CHAR_LITERAL> "}" { retval = new TimestampValue(token.image); }

	| retval = IntervalExpression()
)

[ "::" type=ColDataType() {
		castExpr = new CastExpression();
		castExpr.setUseCastKeyword(false);
		castExpr.setLeftExpression(retval);
		castExpr.setType(type);
		retval=castExpr;
} ]

    {
		if (sign != null) {
			retval = new SignedExpression(sign.image.charAt(0), retval);
		}
    	return retval;
    }
}

JdbcNamedParameter JdbcNamedParameter() : {
	JdbcNamedParameter parameter = new JdbcNamedParameter();
	Token token;
}
{
        ":" token=<S_IDENTIFIER> { parameter.setName(token.image); }
	{
		return parameter;
	}
}

IntervalExpression IntervalExpression() : {
	IntervalExpression interval = new IntervalExpression();
	Token token;
}
{
	<K_INTERVAL> token=<S_CHAR_LITERAL> { interval.setParameter(token.image); }
	{
		return interval;
	}
}

AnalyticExpression AnalyticExpression() :
{
	AnalyticExpression retval = new AnalyticExpression();
	List<Column> plist = new ArrayList<Column>();
	List<OrderByElement> olist = null;
	Token token = null;
	Column column = null;
	Expression expr = null;
	Expression offset = null;
	Expression defaultValue = null;
    WindowElement windowElement = null;
}
{
	token=<S_IDENTIFIER> { retval.setName(token.image); }
	    "(" [ expr=SimpleExpression() ["," offset=SimpleExpression() ["," defaultValue=SimpleExpression() ]] | "*" { retval.setAllColumns(true); } ] ")" <K_OVER> "("
        [<K_PARTITION> <K_BY> column=Column() {plist.add(column);}  ("," column=Column() {plist.add(column);} )* ]
        [olist=OrderByElements() [windowElement = WindowElement() ] ]
        
	{
		retval.setExpression(expr);
		retval.setOffset(offset);
		retval.setDefaultValue(defaultValue);
		retval.setPartitionByColumns(plist);
		retval.setOrderByElements(olist);
        retval.setWindowElement(windowElement);
	}
	")"
	{
		return retval;
	}
}

WindowElement WindowElement():
{
    WindowElement windowElement = new WindowElement();
    WindowRange range = new WindowRange();
    WindowOffset offset = null;
}
{
    (<K_ROWS> { windowElement.setType(WindowElement.Type.ROWS); }  |  <K_RANGE> { windowElement.setType(WindowElement.Type.RANGE); } )
    ( (
      <K_BETWEEN> { windowElement.setRange(range); } 
      offset = WindowOffset() { range.setStart(offset); } 
      <K_AND> offset = WindowOffset() { range.setEnd(offset); }
      )
    | 
      offset = WindowOffset() { windowElement.setOffset(offset); } 
    )
 
    {
        return windowElement;
    }
}

WindowOffset WindowOffset():
{
    WindowOffset offset = new WindowOffset();
    Expression expr = null;
}
{
    (
  
        <K_UNBOUNDED> (<K_PRECEDING> { offset.setType(WindowOffset.Type.PRECEDING); return offset; } |
   									 <K_FOLLOWING> { offset.setType(WindowOffset.Type.FOLLOWING); return offset; } )
    ) 
    |
    ( <K_CURRENT> <K_ROW> { offset.setType(WindowOffset.Type.CURRENT); return offset;} ) 
    |
    ( expr = SimpleExpression() {
        offset.setType(WindowOffset.Type.EXPR); 
        offset.setExpression(expr); 
        } 
        (<K_PRECEDING> { offset.setType(WindowOffset.Type.PRECEDING); } | <K_FOLLOWING> { offset.setType(WindowOffset.Type.FOLLOWING);  } )
    )
  
    {
        return offset;
    }
} 

ExtractExpression ExtractExpression() :
{
	ExtractExpression retval = new ExtractExpression();
	Token token = null;
	Expression expr = null;
}
{
	<K_EXTRACT>
	"("
		token=<S_IDENTIFIER> { retval.setName(token.image); }
		<K_FROM>
		expr=SimpleExpression() { retval.setExpression(expr); }
	")"
	{
		return retval;
	}
}

CastExpression CastExpression():
{
	CastExpression retval = new CastExpression();
	ColDataType type = null;
	Expression expression = null;
	boolean	useCastKeyword;
}
{
	<K_CAST> "(" expression=SimpleExpression() <K_AS> type=ColDataType() ")" { retval.setUseCastKeyword(true); }

    {
	    retval.setLeftExpression(expression);
	    retval.setType(type);
    	return retval;
    }
}

Expression CaseWhenExpression():
{
	CaseExpression caseExp = new CaseExpression();
	Expression switchExp = null;
	WhenClause clause;
	List whenClauses = new ArrayList();
	Expression elseExp = null;
}
{
	<K_CASE>
	(
    	    ( clause=WhenThenSearchCondition() { whenClauses.add(clause); } )+
             [<K_ELSE> elseExp=SimpleExpression()]
		|
		    switchExp=PrimaryExpression()
             ( clause=WhenThenValue() { whenClauses.add(clause); } )*
             [<K_ELSE> elseExp=SimpleExpression()]
	)
    <K_END>
    {
    	caseExp.setSwitchExpression(switchExp);
        caseExp.setWhenClauses(whenClauses);
		caseExp.setElseExpression(elseExp);
    	return caseExp;
    }
}

WhenClause WhenThenSearchCondition():
{
	WhenClause whenThen = new WhenClause();
	Expression whenExp = null;
	Expression thenExp = null;
}
{
	<K_WHEN> whenExp=Expression() <K_THEN> thenExp=SimpleExpression()
	{
	   whenThen.setWhenExpression(whenExp);
	   whenThen.setThenExpression(thenExp);
	   return whenThen;
	}
}

WhenClause WhenThenValue():
{
	WhenClause whenThen = new WhenClause();
	Expression whenExp = null;
	Expression thenExp = null;
}
{
	<K_WHEN> whenExp=PrimaryExpression() <K_THEN> thenExp=SimpleExpression()
	{
	   whenThen.setWhenExpression(whenExp);
	   whenThen.setThenExpression(thenExp);
	   return whenThen;
	}
}

Function Function():
{
	Function retval = new Function();
	String funcName = null;
	String tmp = null;
	ExpressionList expressionList = null;
}
{
	["{fn" { retval.setEscaped(true); } ]
	(
	    funcName=RelObjectName()
    	|
    	// workaround for replace(string) function (name clash with mysql REPLACE expression)
	    <K_REPLACE> { funcName = "REPLACE"; }
		|
    	// workaround for truncate(string) function (name clash with mysql TRUNCATE expression)
	    <K_TRUNCATE> { funcName = "TRUNCATE"; }
    ) [ "." tmp=RelObjectName() { funcName+= "." + tmp; } ["." tmp=RelObjectName() { funcName+= "." + tmp; }]]
    "(" [ [<K_DISTINCT> { retval.setDistinct(true); } | <K_ALL> { retval.setAllColumns(true); }] (expressionList=SimpleExpressionList() | "*" { retval.setAllColumns(true); }) ] ")"

    ["}"]
    {
	    retval.setParameters(expressionList);
	    retval.setName(funcName);
    	return retval;
    }
}

SubSelect SubSelect():
{
	SelectBody selectBody = null;
}
{
    selectBody=SelectBody()
    {
    	SubSelect subSelect = new SubSelect();
    	subSelect.setSelectBody(selectBody);
    	return subSelect;
    }
}

CreateIndex CreateIndex():
{
	CreateIndex createIndex = new CreateIndex();
	Table table = null;
	List<String> colNames = new ArrayList<String>();
	Token columnName;
	Index index = null;
	String name = null;
	String parameter = null;
}
{
	<K_CREATE>
	( parameter=CreateParameter() )*

	<K_INDEX> name=RelObjectName()
	{
		index = new Index();
		index.setName(name);
		index.setType(parameter);
	}

	<K_ON> table=Table()

	"("
	(columnName=<S_IDENTIFIER>
	|
	columnName=<S_QUOTED_IDENTIFIER>)

	(CreateParameter() | <K_ASC> | <K_DESC>)*
	{
		colNames.add(columnName.image);
	}

	(
		","
		(columnName=<S_IDENTIFIER>
		|
		columnName=<S_QUOTED_IDENTIFIER>)

		(CreateParameter() | <K_ASC> | <K_DESC>)*
		{
			colNames.add(columnName.image);
		}
	)*

	")"
	(CreateParameter() {})*

	{
		index.setColumnsNames(colNames);
		createIndex.setIndex(index);
		createIndex.setTable(table);
		return createIndex;
	}
}

CreateTable CreateTable():
{
	CreateTable createTable = new CreateTable();
	Table table = null;
	List columnDefinitions = new ArrayList();
	List columnSpecs = null;
	List tableOptions = new ArrayList();
	Token columnName;
	Token tk = null;
	Token tk2 = null;
	Token tk3 = null;
	ColDataType colDataType = null;
	String stringList = null;
	ColumnDefinition coldef = null;
	List indexes = new ArrayList();
	List colNames = null;
	Index index = null;
	ForeignKeyIndex fkIndex = null;
	String parameter = null;
	Table fkTable = null;
}
{
	<K_CREATE>
	// TODO:
//	[ [ GLOBAL | LOCAL ] { TEMPORARY | TEMP } ]
	(CreateParameter())*

	<K_TABLE> table=Table()
	[
		"("
		(columnName=<S_IDENTIFIER>
		|
		columnName=<S_QUOTED_IDENTIFIER>)

		colDataType = ColDataType()
		{
			columnSpecs = new ArrayList();
		}

		( parameter=CreateParameter() { columnSpecs.add(parameter); } )*

		{
			coldef = new ColumnDefinition();
			coldef.setColumnName(columnName.image);
			coldef.setColDataType(colDataType);
			if (columnSpecs.size() > 0)
				coldef.setColumnSpecStrings(columnSpecs);
			columnDefinitions.add(coldef);
		}

		(
			","

			(
				(
					tk=<K_INDEX>
						tk3=<S_IDENTIFIER>
						colNames=ColumnsNamesList()
						{
							index = new Index();
							index.setType(tk.image);
							index.setName(tk3.image);
							index.setColumnsNames(colNames);
							indexes.add(index);
						}
				)
				|
				LOOKAHEAD(3) (
                    {
                        index = new NamedConstraint();
                    }
                    [<K_CONSTRAINT> tk3=<S_IDENTIFIER> {index.setName(tk3.image);} ]
					tk=<K_PRIMARY> tk2=<K_KEY>
						colNames=ColumnsNamesList()
						{
							index.setType(tk.image + " " + tk2.image);
							index.setColumnsNames(colNames);
							indexes.add(index);
						}
				)
				|
				(
					tk=<K_KEY>
						tk3=<S_IDENTIFIER>
						colNames=ColumnsNamesList()
						{
							index = new Index();
							index.setType(tk.image);
							index.setName(tk3.image);
							index.setColumnsNames(colNames);
							indexes.add(index);
						}
				)
				|
				(
					{
						fkIndex = new ForeignKeyIndex();
					}
					[<K_CONSTRAINT> tk3=<S_IDENTIFIER> {fkIndex.setName(tk3.image);} ]
					tk=<K_FOREIGN> tk2=<K_KEY>
						colNames=ColumnsNamesList()
						{
							fkIndex.setType(tk.image + " " + tk2.image);
							fkIndex.setColumnsNames(colNames);
						}
						<K_REFERENCES> fkTable=Table() colNames=ColumnsNamesList()
						{
							fkIndex.setTable(fkTable);
							fkIndex.setReferencedColumnNames(colNames);
							indexes.add(fkIndex);
						}
				)
				|
				(
					(columnName=<S_IDENTIFIER>
					|
					columnName=<S_QUOTED_IDENTIFIER>)
					colDataType = ColDataType()
					{
						columnSpecs = new ArrayList();
					}

					( 	parameter=CreateParameter() { columnSpecs.add(parameter); } 	)*

					{
						coldef = new ColumnDefinition();
						coldef.setColumnName(columnName.image);
						coldef.setColDataType(colDataType);
						if (columnSpecs.size() > 0)
							coldef.setColumnSpecStrings(columnSpecs);
						columnDefinitions.add(coldef);
					}
				)
			)
		)*

		")"
		( 	parameter=CreateParameter() { tableOptions.add(parameter); } )*

	]

	{
		createTable.setTable(table);
		if (indexes.size() > 0)
			createTable.setIndexes(indexes);
		if (tableOptions.size() > 0)
			createTable.setTableOptionsStrings(tableOptions);
		if (columnDefinitions.size() > 0)
			createTable.setColumnDefinitions(columnDefinitions);
		return createTable;
	}
}

ColDataType ColDataType():
{
	ColDataType colDataType = new ColDataType();
	Token tk = null;
	Token tk2 = null;
	ArrayList argumentsStringList = new ArrayList();
}
{
	( tk=<K_CHARACTER> tk2=<K_VARYING> { colDataType.setDataType(tk.image + " " + tk2.image); }
	| tk=<S_IDENTIFIER> { colDataType.setDataType(tk.image); } )

	[LOOKAHEAD(2) "(" ( (tk=<S_LONG> | tk=<S_CHAR_LITERAL>) { argumentsStringList.add(tk.image); } ["," {/*argumentsStringList.add(",");*/}] )*    ")"]
    [<K_CHARACTER> <K_SET> tk=<S_IDENTIFIER> { colDataType.setCharacterSet(tk.image); } ]

	{
		if (argumentsStringList.size() > 0)
			colDataType.setArgumentsStringList(argumentsStringList);
		return colDataType;
	}
}

CreateView CreateView():
{
	CreateView createView = new CreateView();
	Table view = null;
	SelectBody select = null;
	List<String> columnNames = null;
}
{
	<K_CREATE>
	[ <K_OR> <K_REPLACE> { createView.setOrReplace(true);} ]
	[ <K_MATERIALIZED> { createView.setMaterialized(true);} ]
	<K_VIEW> view=Table() { createView.setView(view); }
	[ columnNames = ColumnsNamesList() { createView.setColumnNames(columnNames); } ]
	<K_AS>
	(
      LOOKAHEAD(SelectBody()) select=SelectBody() { createView.setSelectBody(select); }
	  |
	  "(" select=SelectBody() ")" { createView.setSelectBody(select); }
    )
	{ return createView; }
}

String CreateParameter():
{
	String retval = null;
	Token tk = null;
}
{
		(
			tk=<S_IDENTIFIER> { retval = tk.image; }
			|
			tk=<K_NULL> { retval = tk.image; }
			|
			tk=<K_NOT> { retval = tk.image; }
			|
			tk=<K_PRIMARY> { retval = tk.image; }
			|
			tk=<K_KEY> { retval = tk.image; }
			|
			tk=<S_CHAR_LITERAL> { retval = tk.image; }
			|
			tk=<S_LONG> { retval = tk.image; }
			|
			tk=<S_DOUBLE> { retval = tk.image; }
			|
			"=" { retval = "="; }
			|
			retval=AList()
		)
	{return retval;}
}

String AList():
{
	StringBuilder retval = new StringBuilder("(");
	Token tk = null;
}
{
	 "("

	 ( (tk=<S_LONG> | tk=<S_DOUBLE> | tk=<S_CHAR_LITERAL> | tk=<S_IDENTIFIER>) { retval.append(tk.image); } ["," {retval.append(",");}] )*

	")"
	{
		retval.append(")");
		return retval.toString();
	}
}

List<String> ColumnsNamesList():
{
	List<String> retval = new ArrayList<String>();
	Token tk = null;
}
{
	 "("

	 	tk=<S_IDENTIFIER> { retval.add(tk.image); }
	 	( "," tk=<S_IDENTIFIER> { retval.add(tk.image); } )*

	")"
	{
		return retval;
	}
}

Drop Drop():
{
	Drop drop = new Drop();
	Token tk = null;
	List<String> dropArgs = new ArrayList<String>();
}
{
	<K_DROP>
	(
		tk=<S_IDENTIFIER>
		|
		tk=<K_TABLE>
		|
		tk=<K_INDEX>
	)
    { drop.setType(tk.image); }
	tk=<S_IDENTIFIER> { drop.setName(tk.image); }
	(tk=<S_IDENTIFIER> { dropArgs.add(tk.image); })*

	{
		if (dropArgs.size() > 0)
			drop.setParameters(dropArgs);
		return drop;
	}
}

Truncate Truncate():
{
	Truncate truncate = new Truncate();
	Table table;
}
{
	<K_TRUNCATE> <K_TABLE>
	table=Table() { truncate.setTable(table); }
	{
		return truncate;
	}
}

Alter Alter():
{
	Alter alter = new Alter();
	Table table;
	Token tk;
	ColDataType dataType;
}
{
	<K_ALTER> <K_TABLE> table=Table() <K_ADD> <K_COLUMN>
	(tk=<S_IDENTIFIER>	| tk=<S_QUOTED_IDENTIFIER>) dataType=ColDataType()

	{
		alter.setTable(table);
		alter.setColumnName(tk.image);
		alter.setDataType(dataType);
		return alter;
	}
}